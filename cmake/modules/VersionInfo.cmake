FIND_PACKAGE(Git)
IF(GIT_FOUND AND NOT FORCE_VERSION)
	# Look for git tag information (e.g. Tagged: "v1.0.0", Non-tagged: "v1.0.0-123-a1b2c3d")
	EXECUTE_PROCESS(
		COMMAND "${GIT_EXECUTABLE}" describe --tags --match v[0-9].[0-9].[0-9]*
		OUTPUT_VARIABLE GIT_TAG
		WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
		TIMEOUT 10
		OUTPUT_STRIP_TRAILING_WHITESPACE)
	STRING(REPLACE "-" ";" TAG_LIST "${GIT_TAG}")
	LIST(LENGTH TAG_LIST TAG_LIST_LENGTH)
	IF(TAG_LIST_LENGTH GREATER 0)
		LIST(GET TAG_LIST 0 FORCE_VERSION)
		STRING(REPLACE "v" "" FORCE_VERSION "${FORCE_VERSION}")
	ENDIF()
	IF(TAG_LIST_LENGTH EQUAL 3)
		# Non-tagged version
		LIST(GET TAG_LIST 1 EXTRA_COMMITS)
		STRING(REPLACE "." ";" VERSION_LIST "${FORCE_VERSION}")
		LIST(LENGTH VERSION_LIST VERSION_LENGTH)
		LIST(GET VERSION_LIST 0 VERSION_MAJOR)
		LIST(GET VERSION_LIST 1 VERSION_MINOR)
		LIST(GET VERSION_LIST 2 VERSION_BUILD)
		SET(VERSION_REVISION 0)
		IF(VERSION_LENGTH GREATER 3)
			LIST(GET VERSION_LIST 3 VERSION_REVISION)
		ENDIF()

		EXECUTE_PROCESS(
			COMMAND "${GIT_EXECUTABLE}" rev-parse --abbrev-ref HEAD
			OUTPUT_VARIABLE BRANCH_NAME
			WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
			OUTPUT_STRIP_TRAILING_WHITESPACE)
		STRING(REGEX MATCH "^stable-${VERSION_MAJOR}\\.${VERSION_MINOR}"
			STABLE_BRANCH "${BRANCH_NAME}")
		IF(STABLE_BRANCH)
			# Stable update, FORCE_VERSION=x.x.x.(x + commits)
			MATH(EXPR VERSION_REVISION
				"${VERSION_REVISION} + ${EXTRA_COMMITS}")
			SET(FORCE_VERSION
	"${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_BUILD}.${VERSION_REVISION}"
			)
		ELSE()
			# Development, FORCE_VERSION=x.x.(x + commits)
			MATH(EXPR VERSION_BUILD
				"${VERSION_BUILD} + ${EXTRA_COMMITS}")
			SET(FORCE_VERSION
			"${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_BUILD}"
			)
		ENDIF()
	ENDIF()
ENDIF()

IF(FORCE_VERSION STREQUAL "internal")
	# Use release info from /CMakeLists.txt
ELSEIF(FORCE_VERSION)
	STRING(REPLACE "." ";" VERSION_LIST "${FORCE_VERSION}")
	LIST(LENGTH VERSION_LIST VERSION_LENGTH)
	LIST(GET VERSION_LIST 0 VERSION_MAJOR)
	LIST(GET VERSION_LIST 1 VERSION_MINOR)
	LIST(GET VERSION_LIST 2 VERSION_BUILD)
	IF(VERSION_LENGTH GREATER 3)
		LIST(GET VERSION_LIST 3 VERSION_REVISION)
	ENDIF()
	SET(VERSION             "${FORCE_VERSION}")
ELSEIF(GIT_FOUND)
	MESSAGE(
"Could not get project version.  Using release info from /CMakeLists.txt"
	)
ELSE()
	MESSAGE("Git not found.  Using release info from /CMakeLists.txt")
ENDIF()



MESSAGE("\n"
	"Configuring ${PROJECT_NAME_UCASE}\n"
	"--------------------------\n"
	"* Project version             : ${VERSION}\n"
	"*   Major version             : ${VERSION_MAJOR}\n"
	"*   Minor version             : ${VERSION_MINOR}\n"
	"*   Build version             : ${VERSION_BUILD}\n"
	"*   Revision version          : ${VERSION_REVISION}\n"
        "*\n\n"
	"Optional Version Usage:\n"
	"--------------------------\n"
	"*   Override version:           -DFORCE_VERSION=x.x.x.x\n"
	"*   Ignore Git information:     -DFORCE_VERSION=internal\n"
)

